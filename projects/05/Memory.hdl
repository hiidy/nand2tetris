// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including RAM, memory mapped screen, and memory mapped keyboard.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 (0x0000 to 0x3FFF) results in accessing the RAM;
 * Access to address 16384 to 24575 (0x4000 to 0x5FFF) results in accessing the Screen memory map;
 * Access to address 24576 (0x6000) results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // 000 0000 0000 0000 => 0x0000
    // 100 0000 0000 0000 => 0x4000
    // 110 0000 0000 0000 => 0x6000
    DMux(in=load, sel=address[14], a=ram16load, b=screenload);
    RAM16K(in=in, load=ram16load, address=address[0..13], out=ramout);
    Screen(in=in, load=screenload, address=address[0..12], out=screenout);
    Keyboard(out=keyboardout);

    // screen or keyboard
    Mux16(a=screenout, b=keyboardout, sel=address[13], out=outsk);
    Mux16(a=ramout, b=outsk, sel=address[14], out=out);
}
